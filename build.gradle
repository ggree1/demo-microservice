plugins {
    id "org.springframework.boot" version "2.1.2.RELEASE"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "net.ltgt.apt" version "0.21"
}

ext.spring_cloud_version = "Greenwich.RELEASE"
ext["lombok.version"] = "1.18.4"
ext.queryDslVersion = "4.2.1"

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {

    apply plugin: "java"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    apply plugin: "net.ltgt.apt"
    apply plugin: "net.ltgt.apt-idea"

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    def defaultEncoding = "UTF-8"
    tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

    javadoc {
        options.encoding = defaultEncoding
        options.addBooleanOption("Xdoclint:none", true)
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}"
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        
        implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
        implementation "org.springframework.cloud:spring-cloud-starter-sleuth"
        implementation "org.springframework.boot:spring-boot-starter-actuator"
        implementation "org.springframework.cloud:spring-cloud-starter-bus-amqp"

        implementation "org.springframework.boot:spring-boot-starter-aop"
        implementation "org.springframework.boot:spring-boot-starter-cache"
        implementation "org.springframework.boot:spring-boot-starter-web"
        implementation "org.ehcache:ehcache"
        implementation "javax.cache:cache-api"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5"
        implementation "commons-io:commons-io:2.6"
        implementation "org.apache.commons:commons-lang3"

        implementation group: "io.springfox", name: "springfox-swagger2", version: "2.9.2"
        implementation group: "io.springfox", name: "springfox-swagger-ui", version: "2.9.2"

        implementation "com.querydsl:querydsl-core:${queryDslVersion}"
        implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
        annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}:jpa",
                            "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final",
                            "javax.annotation:javax.annotation-api:1.3.2",
                            "org.projectlombok:lombok",
                            "org.springframework.boot:spring-boot-configuration-processor"
        
        compileOnly "org.springframework.boot:spring-boot-configuration-processor"
        compileOnly "org.projectlombok:lombok"

        runtime "com.h2database:h2"
        testAnnotationProcessor "org.projectlombok:lombok"
        testCompileOnly "org.projectlombok:lombok"
        testImplementation "org.springframework.boot:spring-boot-starter-test"

    }

    task initSourceFolders {
        sourceSets*.java.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }
        sourceSets*.resources.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }
    }
    
}


task print() {
    doLast{
        println "github push complete!"
    }
}

project(":demo-service1") {
    dependencies {
        implementation project(":demo-core")
    }
}
project(":demo-service2") {
    dependencies {
        implementation project(":demo-core")
    }
}
project(":rest-service") {
    dependencies {
        implementation project(":demo-core")
    }
}

wrapper {
    gradleVersion = "5.1.1"
}
